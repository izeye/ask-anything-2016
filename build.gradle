buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "http://repo.spring.io/libs-release" }
        maven { url "http://repo.spring.io/libs-milestone" }
        maven { url "http://repo.spring.io/snapshot" }
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("org.springframework.build.gradle:propdeps-plugin:$propdepsPluginVersion")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:$asciidoctorPluginVersion")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'propdeps'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'maven-publish'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

repositories {
    jcenter()
    maven { url "http://repo.spring.io/libs-release" }
    maven { url "http://repo.spring.io/libs-milestone" }
    maven { url "http://repo.spring.io/snapshot" }

    // NOTE: Use repositories for `ask-anything-answer-module-ua-analyzer`.
//    maven { url "http://localhost:8092/repository/internal" }
//    maven { url "http://localhost:8092/repository/snapshots" }
    mavenLocal()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-data-elasticsearch")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    // FIXME:
    // Disable temporarily due to ClassCastException related to UserAgentAnswerEngine.
//    compile("org.springframework.boot:spring-boot-devtools")

    compile("org.webjars:webjars-locator")

    compile("org.webjars:bootstrap:${bootstrapVersion}")
    compile("org.webjars:jquery:${jQueryVersion}")

    provided("org.projectlombok:lombok")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")
}

if (project.hasProperty('env') && project.getProperty('env') == 'production') {
    apply from: 'gradle/production.gradle'
}

springBoot {
    executable = true
}

ext {
    apiArtifactVersion = '0.2.0.BUILD-SNAPSHOT'
//    apiArtifactVersion = '0.1.0.RELEASE'

    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    include 'com/ctb/askanything/api/**'
}

task apiSourceJar(type: Jar) {
    from sourceSets.main.allJava
    include 'com/ctb/askanything/api/**'
}

publishing {
    publications {
        api(MavenPublication) {
            groupId 'com.ctb.ask-anything'
            artifactId 'ask-anything-api'
            version apiArtifactVersion

            artifact apiJar

            artifact apiSourceJar {
                classifier 'sources'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username "admin"
                password "secret123"
            }

            if (apiArtifactVersion.endsWith("BUILD-SNAPSHOT")) {
                url "http://localhost:8092/repository/snapshots"
            }
            else {
                url "http://localhost:8092/repository/internal"
            }
        }
    }
}

checkstyle {
    toolVersion = '7.0'
    configProperties = [
            'headerLocation': 'config/checkstyle/checkstyle-header.txt',
            'suppressionsLocation': 'config/checkstyle/checkstyle-suppressions.xml'
    ]
}

jacoco {
    toolVersion = '0.7.7.201606060606'
}
