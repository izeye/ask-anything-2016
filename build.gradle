buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "http://repo.spring.io/libs-release" }
        maven { url "http://repo.spring.io/libs-milestone" }
        maven { url "http://repo.spring.io/snapshot" }
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("org.springframework.build.gradle:propdeps-plugin:$propdepsPluginVersion")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:$asciidoctorPluginVersion")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'propdeps'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

repositories {
    jcenter()
    maven { url "http://repo.spring.io/libs-release" }
    maven { url "http://repo.spring.io/libs-milestone" }
    maven { url "http://repo.spring.io/snapshot" }
    mavenLocal()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-data-elasticsearch")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-devtools")

    compile("org.webjars:webjars-locator")

    compile("org.webjars:bootstrap:${bootstrapVersion}")
    compile("org.webjars:jquery:${jQueryVersion}")

    // NOTE: See https://github.com/izeye/ua-analyzer
    compile("com.izeye.ua-analyzer:ua-analyzer-core:${uaAnalyzerVersion}")

    provided("org.projectlombok:lombok")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")
}

springBoot {
    executable = true
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
}

checkstyle {
    toolVersion = '7.0'
    configProperties = [
            'headerLocation': 'config/checkstyle/checkstyle-header.txt',
            'suppressionsLocation': 'config/checkstyle/checkstyle-suppressions.xml'
    ]
}

jacoco {
    toolVersion = '0.7.7.201606060606'
}
